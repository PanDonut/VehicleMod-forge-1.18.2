buildscript {
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'net.minecraftforge.gradle' version '5.1.+'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

apply plugin: 'org.spongepowered.mixin'

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

sourceSets.main.resources { srcDir 'src/generated/resources' }

minecraft {
    mappings channel: "official", version: "1.18.2"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                vehicle {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                vehicle {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'vehicle', '--all', '--output', file('src/generated/resources/')
            mods {
                vehicle {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "vehicle.refmap.json"
    config "vehicle.mixins.json"
}

repositories {
    maven {
        url = "https://www.cursemaven.com"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.2-40.1.54'

    implementation fg.deobf("curse.maven:posture-${project.posture_file}:${project.posture_project}")
    implementation fg.deobf("curse.maven:framework-${project.framework_file}:${project.framework_project}")
    implementation fg.deobf("curse.maven:controllable-${project.controllable_file}:${project.controllable_project}")
    implementation fg.deobf("curse.maven:configured-${project.configured_file}:${project.configured_project}")
    implementation fg.deobf("curse.maven:catalogue-${project.catalogue_file}:${project.catalogue_project}")

    implementation ("org.spongepowered:mixin:0.8.5") {
        exclude module: "asm-commons"
        exclude module: "asm-tree"
        exclude module: "launchwrapper"
        exclude module: "guava"
        exclude module: "log4j-core"
        exclude module: "gson"
        exclude module: "commons-io"
    }
    annotationProcessor("org.spongepowered:mixin:0.8.5:processor")
}

jar {
    manifest {
        attributes([
            "Specification-Title": "Vehicle Mod",
            "Specification-Vendor": "MrCrayfish",
            "Specification-Version": "1",
            "Implementation-Title": "Vehicle Mod",
            "Implementation-Version": project.version,
            "Implementation-Vendor" : "MrCrayfish",
            "MixinConfigs" : "vehicle.mixins.json",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// Workaround as per https://youtrack.jetbrains.com/issue/IDEA-197469
sourceSets {
    main {
        java {
            srcDirs = ["src/main/java"]
        }
        resources {
            srcDirs = ["src/main/java", "src/main/resources", "src/generated/resources"]
        }
    }

    // at compile time, put resources in same directories as classes
    main.output.resourcesDir = main.java.outputDir
}
